import org.gradle.api.tasks.testing.logging.TestExceptionFormat

import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'org.jetbrains.intellij' version "1.9.0"
    id 'org.jetbrains.changelog' version '1.3.1'
    id 'me.filippov.gradle.jvm.wrapper' version '0.11.0'
}

repositories {
    maven { setUrl("https://cache-redirector.jetbrains.com/maven-central") }
}

apply plugin: 'org.jetbrains.changelog'

group "${pluginGroup}"
version "${pluginVersion}"

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation 'com.google.code.gson:gson:2.9.0'
}

test {
    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
        testImplementation 'org.mockito:mockito-core:4.8.0'
        testImplementation 'org.testng:testng:7.6.1'
        testRuntimeOnly 'org.junit.support:testng-engine:1.0.4'
    }

    systemProperty "LOCAL_ENV_RUN", "true" //For use with 'BaseTestWithSolution' and TestNG
    useJUnitPlatform()
    minHeapSize = "512m"
    maxHeapSize = "1024m"
    testLogging {
        showStandardStreams = true
        exceptionFormat = TestExceptionFormat.FULL
    }
}

intellij {
    type = "${platformType}"
    version = "${platformVersion}"
    downloadSources = false
    instrumentCode = true
}

runIde {
    maxHeapSize = "2G"
    autoReloadPlugins = false
}

changelog {
    version = "${pluginVersion}"
    path = "${project.projectDir}/CHANGELOG.md"
    header = "[${-> version.get()}] - ${new SimpleDateFormat("yyyy-MM-dd").format(new Date())}"
    headerParserRegex = ~/(\d+\.\d+\.\d+[\-\w]*)/
    itemPrefix = "-"
    keepUnreleasedSection = true
    unreleasedTerm = "[Unreleased]"
    groups = ["Notes", "Added", "Changed", "Deprecated", "Removed", "Fixed", "Security"]
}

wrapper {
    gradleVersion = "${gradleVersion}"
}

patchPluginXml {
    changeNotes.set(changelog.getUnreleased().toHTML())
}

publishPlugin {
    token.set(System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken"))
    channels.set(List.of(("${pluginVersion}".split("-").length > 1) ? "${pluginVersion}".split("-")[1] : "default"))
    var version = changelog.getOrNull("${pluginVersion}")
    if (version != null) {
        patchPluginXml.changeNotes.set(version.toHTML())
    }
}